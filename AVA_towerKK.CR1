'CR1000
'Created by Brian Harvey: 01/16/2019
'Used Program from Celestino Tower as template
'----Modified by Sergio dos santos 02/12/2019
'Changed wind-speed to km/hr to be easily compared w/Lutz tower readings
'According to new instructions on 05103 Anemometer the settling time should be 20,000 micro-s
'Reading PBID directly from the logger
'----Modified by Sergio dos Santos 02/18/2019
'Removed Wind-Speed average - already on the Wind Vector speed
'Removed Min wind Speed Minimum - Not very usefull
'Added Max & Min for SR and PAR
'Added Batt_voltage to 5min table
'Eliminated 1hour Table2
'-----Modified by Sergio dos Santos 03/11/2019
'Placed the power for 40m EE181 T&RH on Swicthed 12V
'-----Modified by Sergio dos Santos 3/28/2019
'Changed field name for tipping bucket to FieldNames("Rain_mm_Tot")
'-----Modified by Brian Harvey 12/5/2019
'Added EE181 T&RH Sensor @ 2m and removed comments for 20m T&RH
'-----Modified by Sergio 11/12/2020
'Adapted the code for T&RH HMP60 sensor @40m
'Replaced HMP60 @40m and North/South PAR sensors 5/31/2021 - Brian Harvey
'Replaced North and South Pyranometers and PAR sensors 2/22/2022 - Brian Harvey
'Replaced TB, 40m Temp, and North Pyranometer 3/8/2022 - Brian Harvey


'Declare Variables and Units
Public BattV
'T&RH variables
'T&RH 2m
Public AirTC_2m
Public RH_2m
'T&RH 40m
Public TRHData_40m(2)
'Wind variables
Public WS_kph
Public WindDir
'Solar radiation variables
Public SlrW
Public SlrW_2
'PAR Variables
Public PAR
Public PAR_2
'Rain
Public RainTB3
'Pakbus ID
Public PBID As Long

Alias TRHData_40m(1)=AirTC_40m
Alias TRHData_40m(2)=RH_40m

'Units declarations

Units BattV=Volts
Units AirTC_2m=Deg C
Units RH_2m=%
Units AirTC_40m=Deg C
Units RH_40m=%
Units WS_kph=kilometers/hour
Units WindDir=Degrees
Units SlrW=W/m^2
Units SlrW_2=W/m^2
Units PAR=umol/s*m^2
Units PAR_2=umol/s*m^2
Units RainTB3=mm


'Data Table 1 - 5 minute readings
DataTable(Table1,True,-1)
	DataInterval(0,5,Min,10)
	
	Sample(1,PBID,UINT2)
	FieldNames("PBID")
  Minimum(1,BattV,FP2,False,False)
	FieldNames("BattV_Min")
		
	
	Maximum(1,AirTC_2m,FP2,False,False)
	FieldNames("AirTC_2m_Max")
	Minimum(1,AirTC_2m,FP2,False,False)
	FieldNames("AirTC_2m_Min")
	Average(1,AirTC_2m,FP2,False)
	FieldNames("AirTC_2m_Avg")
	
	Maximum(1,RH_2m,FP2,False,False)
	FieldNames("RH_2m_Max")
	Minimum(1,RH_2m,FP2,False,False)
	FieldNames("RH_2m_Min")
	Average(1,RH_2m,FP2,False)
	FieldNames("RH_2m_Avg")
	
	Maximum(1,AirTC_40m,FP2,False,False)
	FieldNames("AirTC_40m_Max")
	Minimum(1,AirTC_40m,FP2,False,False)
	FieldNames("AirTC_40m_Min")
	Average(1,AirTC_40m,FP2,False)
	FieldNames("AirTC_40m_Avg")
	
	Maximum(1,RH_40m,FP2,False,False)
	FieldNames("RH_40m_Max")
	Minimum(1,RH_40m,FP2,False,False)
	FieldNames("RH_40m_Min")
	Average(1,RH_40m,FP2,False)
	FieldNames("RH_40m_Avg")
	
 'Wind
	Maximum(1,WS_kph,FP2,False,False)
	FieldNames("WS_kph_Max")
	WindVector (1,WS_kph,WindDir,FP2,False,0,0,0)
	FieldNames("WS_kph_S_WVT,WindDir_D1_WVT,WindDir_SD1_WVT")
	
 'SR North
	Maximum (1,SlrW,FP2,False,False)
	FieldNames("SlrW_N_Max")
	Minimum(1,SlrW,FP2,False,False)
	FieldNames("SlrW_N_Min")
  Average(1,SlrW,FP2,False)
	FieldNames("SlrW_N_Avg")
	'SR South
  Maximum (1,SlrW_2,FP2,False,False)
  FieldNames("SlrW_S_Max")
  Minimum (1,SlrW_2,FP2,False,False)
  FieldNames("SlrW_S_Min")
	Average(1,SlrW_2,FP2,False)
	FieldNames("SlrW_S_Avg")
	
 'PAR North
  Maximum (1,PAR,FP2,False,False)
  FieldNames("PARDen_N_Max")
  Minimum (1,PAR,FP2,False,False)
  FieldNames("PARDen_N_Min")
	Average(1,PAR,FP2,False)
	FieldNames("PARDen_N_Avg")
 'PAR South
  Maximum (1,PAR_2,FP2,False,False)
  FieldNames("PARDen_S_Max")
  Minimum (1,PAR_2,FP2,False,False)
  FieldNames("PARDen_S_Min")
	Average(1,PAR_2,FP2,False)
	FieldNames("PARDen_S_Avg")
	
	Totalize(1,RainTB3,FP2,False)
	FieldNames("Rain_mm_Tot")
		
EndTable


'Main Program
BeginProg
	Scan(10,Sec,1,0)
		'Default Datalogger Battery Voltage measurement BattV
		Battery(BattV)
	
    'Temperature and Relative Humidity CSEE181
		'T&RH 2m (EE181 sensor, T-SE11, RH-SE12) Power w/12V
		VoltSe(AirTC_2m,1,mv2500C,11,0,0,_60Hz,0.1,-40.0)
		VoltSe(RH_2m,1,mv2500C,12,0,0,_60Hz,0.1,0)
		If RH_2m>100 AND RH_2m<108 Then RH_2m=100
		
		'T&RH 40m (CS215) 
		SDI12Recorder(TRHData_40m(),1,0,"M!",1,0)
		If RH_40m>100 AND RH_40m<108 Then RH_40m=100

		
		'Wind Speed & Direction Sensor measurements WS_ms and WindDir
		PulseCount(WS_kph,1,1,1,1,0.3528,0)
		BrHalf(WindDir,1,mV2500,15,1,1,2500,True,20000,_60Hz,355,0)
		If WindDir>=360 Then WindDir=0
				
		'CMP3(19.70)Pyranometer measurements SlrW (North)
		VoltDiff(SlrW,1,mV250,1,True,0,_60Hz,1,0)
		If SlrW<0 Then SlrW=0
		SlrW=SlrW*(1000/19.70)
	
		'CMP10(9.24) Pyranometer measurements SlrW_2 (South)
		VoltDiff(SlrW_2,1,mV25,2,True,0,_60Hz,1,0)
		If SlrW_2<0 Then SlrW_2=0
		SlrW_2=SlrW_2*(1000/9.24)
		
		'PSQ1 Quantum Sensor measurements PAR (North)
		'Output 5.13 uvolt/umol/s*m^2
		VoltDiff(PAR,1,mV250,3,True,0,_60Hz,1,0)
		If PAR<0 Then PAR=0
		PAR=(PAR*1000)/5.13
		
		'PSQ1 Quantum Sensor measurements PAR_2 (South)
		'Output 4.95 uvolt/umol/s*m^2 
		VoltDiff(PAR_2,1,mV250,4,True,0,_60Hz,1,0)
		If PAR_2<0 Then PAR_2=0
		PAR_2= (PAR_2*1000)/4.95
		
		'TB3 Rain Gauge measurements
		PulseCount(RainTB3,1,2,2,0,0.254,0)
			
		'PackBus ID
		 PBID=Status.PakBusAddress
		
		'Call Data Tables and Store Data
		CallTable(Table1)

	NextScan
EndProg
