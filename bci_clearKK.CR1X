'CR1000X Series
'Adapted from .CR1 clearing on 2022-03-18 by Sergio dos Santos
'Sensors: HMP60,TB3(CS700),CS106(PTB110 vaisala) & CCFC WEBCAM Evap
'The port (C1) used for the CS106 was set to high prior to the reading and then set to low after the reading 
'The first program that was used with MySQL - Added the a PBID variable to record the pakbus ID of the site
'Edited by Brian Harvey on 02/15/2016: Changed the multiplier and offset for the pressure sensor to 0.2 and 600 respectively
'Edited by Brian Harvey on 10/14/2020: Replaced CS215 Temp/RH for HMP60 Temp/RH
'Edited by Brian Harvey on 02/18/2022: Changed multiplier and offset for Vaisala PTB110/CS106 barometer; 0.24 and 500 respectively
'Edited by Sergio 3/21/2022: Removed the 2s delay a turn off of bar sensor, otherwise it may affect RA tips that my occur during the delay

'Declare Variables and Units
Public Batt_Volt
Public AirTC
Public RH
Public BP_mmHg
Public Rain_mm
'Declare a var for PBID
Public PBID

'Declare Units
Units Batt_Volt=Volts
Units AirTC=Deg C
Units RH=%
Units BP_mmHg=mmHg
Units Rain_mm=mm

'Define Data Tables
DataTable(bci_clear,True,-1)
	DataInterval(0,15,Min,10)
	Sample (1,PBID,UINT2)	
	Maximum(1,AirTC,FP2,False,False)
	FieldNames("AirTC_MAX")
	Minimum(1,AirTC,FP2,False,False)
	FieldNames("AirTC_MIN")
	Average(1,AirTC,FP2,False)
	FieldNames("AirTC_AVG")
	Maximum(1,RH,FP2,False,False)
	FieldNames("RH_MAX")
	Minimum(1,RH,FP2,False,False)
	FieldNames("RH_MIN")
	Average(1,RH,FP2,False)
	FieldNames("RH_AVG")
	Average(1,BP_mmHg,FP2,False)
	FieldNames("BP_mmHg")
EndTable

DataTable(bci_clearra,True,-1)
	DataInterval(0,5,Min,10)
	Sample(1,PBID,UINT2)	
	Average (1,Batt_Volt,FP2,False)
	FieldNames("BattV_Avg")	
	Totalize(1,Rain_mm,FP2,False)
	FieldNames("Rain_mm_tot")
EndTable

'Main Program
BeginProg
	Scan(10,Sec,1,0)
		'Default Datalogger Battery Voltage measurement Batt_Volt:
		Battery(Batt_Volt)
		
		'HMP60 T&RH Sensor measurements AirTC AND RH:
		VoltSe(AirTC,1,mV1000,5,0,0,_60Hz,0.1,-40)
		VoltSe(RH,1,mV1000,6,0,0,_60Hz,0.1,0)
		If (RH>100) AND (RH<108) Then RH=100
		
    'CS700 Rain Gauge measurement Rain_mm:
		PulseCount(Rain_mm,1,P1,1,0,0.254,0)
		
		'CS106(PTB110) Barometric Pressure Sensor measurement BP_mmHg:
		PortSet(C1,1) 'Set port 1 (C1) to high - turns on the BP sensor
		'Power lines in Panama are 60Hz - use a _60Hz noise filter
		'The following line will make a single ended measurement (H1) of bar pressure in mb   
		VoltSe(BP_mmHg,1,mV5000,1,1,0,_60Hz,0.24,500)
		'Multiply the above line by 0.75006 to get pressure in mmHg
		BP_mmHg=BP_mmHg*0.75006
				
		'User Entered Calculations
		PBID = Status.PakBusAddress
		
		'Call Data Tables and Store Data
		CallTable(bci_clear)
		CallTable(bci_clearra)
	NextScan
EndProg
